4  REM  DISABLE 80-COLUMNS:
5  PRINT CHR$(21)
10 HOME
20 REM ** PRINT ALL CHARACTERS
30 REM    FROM 0 TO 255,
40 REM    THEN EXIT.
100 LET C=0
120 FOR L=0 TO 15 : REM LINES
130 GOSUB 500 : REM  CALC BAS
150 FOR I=0 TO 15 : REM COLS
155 REM  NEXT LINE AVOIDS OUTP FILTERS
160 POKE BAS+I, C
170 C=C+1
180 NEXT I
200 NEXT L
220 LET ALT=0
230 POKE 49166+ALT,0:REM TOGGLE ALTCHAR
250 VTAB 18
252 IF ALT=0 THEN ? "NORMAL   ":GOTO 256
255 ? "ALTERNATE"
256 ?:? "TYPE Q TO QUIT"
257 ? "ANYTHING ELSE TO TOGGLE ALT CHARS"
260 ?:GET A$:A=ASC(A$)
270 IF A=81 OR A=113 THEN STOP
300 ALT = NOT ALT : GOTO 230
500 REM ** CALCULATE LINE BASE
510 REM    FROM LINE NUMBER
530 REM    (ONLY WORKS FOR TOP 16 ROWS)
550 LET BAS = 1024 + L * 128
560 IF BAS >= 2048 THEN BAS = BAS - 1024 + 40
580 RETURN
